apply plugin: 'com.android.library'

version = project.VERSION_NAME
group = project.GROUP

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
        versionName getVersionName(project.VERSION_NAME)
        versionCode Integer.parseInt(project.VERSION_CODE)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    resourcePrefix 'Un7z'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided "com.android.support:support-annotations:${support_version}"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def getVersionName(String versionName) {
    if (isRelease()) {
        if (versionName.endsWith('-SNAPSHOT')) {
            versionName = versionName.replace("-SNAPSHOT", "")
        }
    } else {
        if (!versionName.endsWith('-SNAPSHOT')) {
            versionName = versionName + "-SNAPSHOT"
        }
    }
    println 'VersionName=' + versionName;
    return versionName
}

def isRelease() {
    if (hasProperty('RELEASE')) {
        return getProperty('RELEASE').toBoolean()
    }
    return false
}

apply from: 'bintray.gradle'
